buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id "com.jfrog.bintray" version "1.8.4"
    id 'net.researchgate.release' version '2.6.0'
}

group = "org.grobid"

description = """The goal of this GROBID module is to recognize in textual documents any expressions of 
measurements (e.g. pressure, temperature, etc.), to parse and normalization them, and finally to convert 
these measurements into SI units. We focus our work on technical and scientific articles (text, XML and PDF input) 
and patents (text and XML input)."""

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
tasks.run.workingDir = rootProject.rootDir

ext {
    // treating them separately, these jars will be flattened into grobid-core.jar on installing,
    // to avoid missing dependencies from the projects that include grobid-core (see 'jar' taskin grobid-core)
    localLibs = []
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://oss.jfrog.org/artifactory/libs-release" }
}

dependencies {
    //Tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.easymock:easymock:4.0.2'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.easymock:easymock:3.4'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation 'org.powermock:powermock-api-easymock:2.0.2'

    //GROBID
    implementation 'com.github.kermitt2:grobid:0.7.0'
//    compile('org.grobid:grobid-service:0.6.2') {
//        transitive = false
//    }

    //Apache commons
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'org.apache.commons:commons-lang3:3.6'
    implementation 'commons-logging:commons-logging:1.2'
    implementation 'commons-io:commons-io:2.6'
    implementation 'commons-pool:commons-pool:1.6'

    //Json
    implementation "com.fasterxml.jackson.core:jackson-core:2.10.1"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.10.1"
    implementation "com.fasterxml.jackson.module:jackson-module-afterburner:2.10.1"

    // measurements
    implementation group: 'si.uom', name: 'si-units-java8', version: '0.9'
    implementation group: 'si.uom', name: 'si-quantity', version: '0.9'
    implementation group: 'tec.uom', name: 'uom-se', version: '1.0.9'
    implementation group: 'systems.uom', name: 'systems-ucum-java8', version: '0.9'
    implementation group: 'systems.uom', name: 'systems-unicode-java8', version: '0.9'
    implementation group: 'systems.uom', name: 'systems-quantity', version: '0.9'
    implementation group: 'systems.uom', name: 'systems-common', version: '0.9'

//    implementation 'tech.units:indriya:1.3'
//    implementation group: 'si.uom', name: 'si-units', version: '1.2'
//    implementation group: 'si.uom', name: 'si-quantity', version: '1.2'
//    implementation group: 'systems.uom', name: 'systems-quantity', version: '1.0'
//    implementation group: 'systems.uom', name: 'systems-common', version: '1.0'
//    implementation group: 'systems.uom', name: 'systems-unicode', version: '1.0'
//    implementation group: 'systems.uom', name: 'systems-ucum', version: '1.0'
//    implementation group: 'systems.uom', name: 'systems-ucum-java8', version: '1.0'
//    implementation group: 'systems.uom', name: 'systems-unicode-java8', version: '1.0'

    //Dropwizard
    implementation "io.dropwizard:dropwizard-core:1.3.29"
    implementation 'io.dropwizard:dropwizard-jersey:1.3.29'
    implementation "io.dropwizard:dropwizard-assets:1.3.29"
    implementation "com.hubspot.dropwizard:dropwizard-guicier:1.3.5.2"
    implementation "io.dropwizard:dropwizard-testing:1.3.29"
    implementation "io.dropwizard:dropwizard-forms:1.3.29"
    implementation "io.dropwizard:dropwizard-client:1.3.29"
    implementation "io.dropwizard:dropwizard-auth:1.3.29"
    implementation "io.dropwizard.metrics:metrics-core:4.0.0"
    implementation "io.dropwizard.metrics:metrics-servlets:4.0.0"
    implementation 'javax.servlet:javax.servlet-api:3.1.0'

    //Misc
    implementation group: 'com.googlecode.clearnlp', name: 'clearnlp', version: '1.3.1'
    implementation "com.google.guava:guava:28.2-jre"
    implementation group: 'net.arnx', name: 'jsonic', version: '1.3.10'

    // XML
    implementation 'org.codehaus.woodstox:stax2-api:3.1.4'
    implementation 'com.fasterxml.woodstox:woodstox-core:5.1.0'


    // Needed for compatibility with JDK > 8
    implementation 'javax.activation:activation:1.1.1'
}

configurations {
    compile.exclude group: "org.slf4j", module: "slf4j-jdk14"
    compile.exclude group: 'org.slf4j', module: "slf4j-log4j12"
    compile.exclude group: 'log4j', module: "log4j"
}

configurations.all {
    resolutionStrategy {
        force 'xml-apis:xml-apis:1.4.01'
    }
}

test {
    exclude '**/**IntegrationTest**'
}

task integration(type: Test) {
    include '**'
}

// Training configuration

def trainerTasks = [
        //Training models
        "train_units"            : "org.grobid.trainer.UnitTrainer",
        "train_quantities"       : "org.grobid.trainer.QuantitiesTrainer",
        "train_values"           : "org.grobid.trainer.ValueTrainer",
        "train_quantifiedObjects": "org.grobid.trainer.QuantifiedObjectTrainer"
]

trainerTasks.each { taskName, mainClassName ->
    tasks.create(name: taskName, type: JavaExec, group: 'training') {
        main = mainClassName
        classpath = sourceSets.main.runtimeClasspath
    }
}

// return the default value if the property has not been specified in command line
ext.getArg = { propName, defaultVal ->
    return project.hasProperty(propName) ? project.getProperty(propName) : defaultVal;
}

/*** Packaging and distribution ***/

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

task mainJar(type: ShadowJar, group: 'onejar') {
    zip64 true
    from sourceSets.main.output
    mergeServiceFiles()
    from {
        project.configurations.compile.collect {
            it.isDirectory() ? [] : localLibs.contains(it.getName()) ? zipTree(it) : []
        }
    }
}

shadowJar {
    mainClassName = 'org.grobid.service.main.GrobidQuantitiesApplication'
    archiveClassifier = 'onejar'
    mergeServiceFiles()
    zip64 true
    manifest {
        attributes 'Main-Class': mainClassName
    }
    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath]
}

jar {
    dependsOn mainJar
}

artifacts {
    archives shadowJar
}

distZip.enabled = false
distTar.enabled = false
shadowDistZip.enabled = false
shadowDistTar.enabled = false

/** Model management **/
task copyModels(type: Copy) {
    from "${rootDir}/resources/models"
    include "**/*.wapiti"
    include "**/config.json"
    include "**/model_weights.hdf5"
    include "**/preprocessor.json"
    into "${rootDir}/../grobid-home/models/"
}

wrapper {
    gradleVersion "7.0.2"
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

coveralls {
    jacocoReportPath 'build/reports/jacoco/test/jacocoTestReport.xml'
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}




